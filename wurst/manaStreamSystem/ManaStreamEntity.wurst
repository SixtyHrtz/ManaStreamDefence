package ManaStreamEntity
import ClosureEvents
import GeneratedBuildings
import HashList
import HashMap
import LinkedList
import ManaStream
import ManaStreamer

public HashMap<int, ManaStreamEntity> manaStreamEntities

public class ManaStreamEntity
    unit entity    

    protected bool isActive
    protected int manaSupply

    protected LinkedList<ManaStream> manaStreams

    construct(unit entity)
        this.entity = entity

        manaStreams = new LinkedList<ManaStream>

        EventListener.add(entity, EVENT_PLAYER_UNIT_DEATH) () ->
            removeManaStreams()

    function addManaStream(unit caster, unit target, bool isDirect)
        manaStreams.add(new ManaStream(caster, target, isDirect))

    function removeManaStreams()
        for manaStream in manaStreams
            manaStream.kill(true)
        manaStreams.clear()

    function removeManaStream(ManaStreamEntity target, bool firstPass)
        for manaStream in manaStreams
            if manaStream.targetEntity == target
                manaStream.kill(firstPass)
                manaStreams.remove(manaStream)

    function getCircuitPower() returns int
        let inspected = new HashList<ManaStreamEntity>()
        return dfs(inspected)

    function dfs(HashList<ManaStreamEntity> inspected) returns int
        if inspected.has(this)
            return 0
        inspected.add(this)

        var totalMana = manaSupply
        for manaStream in manaStreams
            totalMana += manaStream.targetEntity.dfs(inspected)

        return totalMana

public function initManaStreamEntities()
    manaStreamEntities = new HashMap<int, ManaStreamEntity>()

    EventListener.add(EVENT_PLAYER_UNIT_CONSTRUCT_FINISH) () ->
        let entity = GetConstructedStructure()

        ManaStreamEntity streamEntity = null
        switch GetConstructedStructure().getTypeId()
            case MANA_GENERATOR_ID
                streamEntity = new ManaStreamer(entity)
                streamEntity.manaSupply = 100
            case MANA_TOWER_ID
                streamEntity = new ManaStreamEntity(entity)
                streamEntity.manaSupply = -50
        
        if (streamEntity != null)
            manaStreamEntities.put(entity.getHandleId(), streamEntity)

public function unit.getStreamEntity() returns ManaStreamEntity
    let targetHandleId = this.getHandleId()
    if not manaStreamEntities.has(targetHandleId)
        return null

    return manaStreamEntities.get(targetHandleId)
